<script src=lib1.js></script>

<body bgcolor=black>
<center>
<td><canvas id='canvas1' width=800 height=800></canvas></td>
</center>
</body>

<script id='my_vertex_shader' type='x-shader/x-vertex'>
   attribute vec3 aPosition;
   varying   vec3 vPosition;
   void main() {
      gl_Position = vec4(aPosition, 1.0);
      vPosition = aPosition;
   }
</script>

<script id='my_fragment_shader' type='x-shader/x-fragment'>
   precision mediump float;
   uniform float uTime;
   uniform vec3  uCursor;
   varying vec3  vPosition;

  vec4 sphere[3], material[3];
  vec3 lightDir[2], lightColor[2];

  float raySphere(vec3 V, vec3 W, vec4 s)
  {
   float b = 2. * dot(V -= s.xyz, W);
   float c = dot(V,V) - s.w * s.w;
   float d = b * b - 4. * c;
   return d < 0. ? 10000. : (-b - sqrt(d)) / 2.;
  }

  int checkSphere(vec3 V, vec3 W)
  {
   int I = -1;
   float t, T = 1000.;
   for (int i = 0; i < 3; i++)
   {
      if ( (t = raySphere(V,W, sphere[i])) > 0. && t < T)
      {
         I = i;
         T = t;
      }
   }
   return I;
  }

  vec3 shadeSphere(vec3 W, vec3 S, vec4 s, vec4 m)
  {
   vec3 normal = (S - s.xyz) / s.w;
   vec3 R = W - 2. * normal * dot(W,normal);
   vec3 color = m.rgb * .2;
   for (int i = 0; i < 2; i++)
   {
      if (checkSphere(S + 0.01 * lightDir[i], lightDir[i]) < 0)
      {
         float d = pow(max(0., dot(lightDir[i], normal)), 1.5);
         float s = pow(max(0., dot(lightDir[i], R)), m.a);
         color += lightColor[i] * (m.rgb * d + min(1., m.a / 20.) * s);
      }
   }
   return color;
  }

  vec3 rayTrace(vec3 V, vec3 W)
  {
   int I = checkSphere(V,W);
   if (I == -1) return vec3(0.,0.,0.);
   vec4 s = I==0 ? sphere [0] : I==1 ? sphere[1] : sphere[2];
   vec4 m = I==0 ? material [0] : I==1 ? material[1] : material[2];
   return shadeSphere(W, V + W * raySphere(V,W,s), s, m);
  }

  void main(void)
  {
   lightDir[0] = normalize(vec3(sin(uTime * cos(uTime/2.))*0.7,0.1,0.5*abs(sin(uTime*0.1))));
   lightDir[1] = normalize( vec3(uCursor.x, uCursor.y, .5) );
   lightColor[0] = vec3(5.0, 0.8, 0.3); //change
   lightColor[1] = vec3(.0,1.,.0);
   sphere[0] = vec4( 3.*sin(uTime) , 3.*cos(uTime)  , -10. + sin(uTime * 1.5) , 2.);
   sphere[1] = vec4(.3*cos(.8*uTime), 0., -1.4 + 1.3 * sin(.8*uTime), 0.1);
   sphere[2] = vec4( 3.*sin(uTime / 1.5) , 5.*sin(uTime + 3.)  , -10. + cos(uTime * 1.5) , 1.5*cos(uTime / 4.) + 2.);
   material[0] = vec4(.8, .1, .5, 15.);
   material[1] = vec4(.0 , .25, .75, 20.);
   material[2] = vec4(.2, .4, .3, 10.);
   vec3 W = normalize(vec3(vPosition.x, vPosition.y, -0.5));
   vec3 color = rayTrace(vec3(0.,0.,0.), W);
   color = pow(color, vec3(.45,.45,.45));
   gl_FragColor = vec4(color, 1.);

  }
   
</script>

<script>
start_gl('canvas1', document.getElementById('my_vertex_shader'  ).innerHTML,
                    document.getElementById('my_fragment_shader').innerHTML);
</script>

